 
model {

# Model: cctocl_dynamic_int1_072023_gof
#
# initial occupancy (psi1) = habitat + popden + habitat:popden + re(area)
# colonization (gamma) = season + popden + season:popden + re(area)
# desertion (epsilon) = season + popden + season:popden + re(area)
# detection (p) = season + habitat + season:habitat + re(area)
# -----------------------------------------------------------------------------------------
#
# Parameters:
#
# psi1: initial occupancy probability
# gamma: colonization probability
# epsilon: desertion probability
# p: detection probability
# -----------------------------------------------------------------------------------------
#
# Others:
#
# narea: number of study areas, 10
# nsite: number of camera-trap stations, 171
# nprimary: total number of primary sampling periods, seasons, 22
# nobs: number of observations
# y: detection history
# z: true latent state variable, z=1 means occupied, z=0 not occupied.
# n.occ: number of occupied sites
# n.prop: proportion of occupied sites to all total sites
#
# area: study areas
# season: seasons, 2-level categorical, winter(1) and summer (0)
# popden: rural human population density, continuous, standardized
# elevation: meter, continuous, standardized
# habitat: habitat type, 4-level categorical:
## Broad Leaved Forest (BL) = 1
## Coniferous Forest (CF) = 2 
## Mixed Forest (MF) = 3
## Human Land-Use Areas (O) = 4
# 
# -----------------------------------------------------------------------------------------

## Model for Capreolus capreolus - Roe Deer (dominant species)
##############################################################

#### PRIORS ####

# psi1 ~ habitat + popden + habitat:popden + re.psi1(area)

for (h in 1:4) {
  alphapsi.cc[h] ~ dlogis(0, 1)                                    # intercept for each habitat type(4)
  
  # betap values should be according to habitat types (AHM II Chp15 pp.104-105)
  # we have 4 habitat types --> 4 betapsi values for 1 betapsi category
  # we have 1 betapsi category: popden effect
  
  betapsi.cc[h] ~ dnorm(0, 1)                                      # prior for popden coefficient w/ habitat types
}

# gamma ~ season + popden + elevation + re.gamma(area)

alphagamma.cc ~ dlogis(0, 1)                                       # intercept

for(i in 1:3){                                                  # prior for coefficients 
  betagamma.cc[i] ~ dnorm(0, 1)
}

# eps ~ season + popden + elevation + re.eps(area)

alphaeps.cc ~ dlogis(0, 1)                                         # intercept

for(i in 1:3){                                                  # prior for coefficients 
  betaeps.cc[i] ~ dnorm(0, 1)
}

# p ~ season*habitat

for (h in 1:4) {
  # alphap.cc[h] ~ dlogis(0, 1)                                      # intercept for each habitat type(4)
  
  alphap.cc[h] ~ dlogis(-2, 1)
    
  # betap values should be according to habitat types (AHM II Chp15 pp.104-105)
  # we have 4 habitat types --> 4 betap values for 1 betap category
  # we have 1 betap category: betap[p] season effect
  
  betap.cc[h] ~ dnorm(0, 1)                                        # betap season effect
}

# Random effect priors
  
for (t in 1:(narea)){
randompsi.cc[t] ~ dnorm(0, taupsi.cc)
randomgamma.cc[t] ~ dnorm(0, taugamma.cc)
randomeps.cc[t] ~ dnorm(0, taueps.cc)
randomp.cc[t] ~ dnorm(0, taup.cc)

    
}

sigmapsi.cc ~ dnorm(0, 1)T(0,)                    # Priors for standard deviations
sigmagamma.cc ~ dnorm(0, 1)T(0,)
sigmaeps.cc ~ dnorm(0, 1)T(0,)
sigmap.cc ~ dnorm(0, 1)T(0,)

taupsi.cc <- pow(sigmapsi.cc, -2)
taugamma.cc <- pow(sigmagamma.cc, -2)
taueps.cc <- pow(sigmaeps.cc, -2)
taup.cc <- pow(sigmap.cc, -2)

sigma2psi.cc <- pow(sigmapsi.cc, 2)                  # Temporal variances
sigma2gamma.cc <- pow(sigmagamma.cc, 2)
sigma2eps.cc <- pow(sigmaeps.cc, 2)
sigma2p.cc <- pow(sigmap.cc, 2)

#### MODELS ####

# Ecological submodel: intial occupancy as derived parameter

for (i in 1:nsite){
  z.cc[i,1] ~ dbern(psi1.cc[i])
  logit(psi1.cc[i]) <- alphapsi.cc[habitat[i]] 
  + betapsi.cc[habitat[i]]*popden[i]
  + randompsi.cc[area[i]]
}

# State transitions: colonization and extinction
# for col and ext

for (i in 1:nsite){
  for (s in 2:nprimary){
    logit(gamma.cc[i,s-1]) <- alphagamma.cc 
    + betagamma.cc[1]*season[i,s-1] + betagamma.cc[2]*popden[i] + betagamma.cc[3]*season[i,s-1]*popden[i]
    + randomgamma.cc[area[i]]
    
    logit(eps.cc[i,s-1]) <- alphaeps.cc 
    + betaeps.cc[1]*season[i,s-1] + betaeps.cc[2]*popden[i] + betaeps.cc[3]*season[i,s-1]*popden[i]
    + randomeps.cc[area[i]]
  }
}

# for z: true occupancy

for(i in 1:nsite){
  for (t in 2:nprimary){
    
    z.cc[i,t] ~ dbern((z.cc[i,t-1]*(1-eps.cc[i,t-1])) + ((1-z.cc[i,t-1])*gamma.cc[i,t-1]))
  }
}

# Observation model 

for (i in 1:nobs.cc){
  logit(p.cc[i]) <- alphap.cc[habitat.p.cc[i]]
  + betap.cc[habitat.p.cc[i]]*season.p.cc[site.cc[i]]
  + randomp.cc[area.cc[i]]
  
   #y.cc[i] ~ dbern(z.cc[site.cc[i],pocc.cc[i]]*p.cc[i])  # y: observed occupancy
  
   pstar.cc[i] <- 1-(1-p.cc[i])^ndays.cc[i] 
   y.cc[i] ~ dbern(z.cc[site.cc[i],pocc.cc[i]]*pstar.cc[i])  # y: observed occupancy
}

## Model for Canis lupus - Gray wolf (sub-ordinate species)
###########################################################

#### PRIORS ####

for (k in 1:2){                                               # Presence/absence of dominant species
  
  # psi1 ~ habitat + popden + habitat:popden + re.psi1(area)
  
  for (h in 1:4) {
    alphapsi.cl[k,h] ~ dlogis(0, 1)                                    # intercept for each habitat type(4)
    
    # betap values should be according to habitat types (AHM II Chp15 pp.104-105)
    # we have 4 habitat types --> 4 betapsi values for 1 betapsi category
    # we have 1 betapsi category: popden effect
    
    betapsi.cl[k,h] ~ dnorm(0, 1)                                      # prior for popden coefficient w/ habitat types
  }
  
  # gamma ~ season + popden + elevation + re.gamma(area)
  
  alphagamma.cl[k] ~ dlogis(0, 1)                                       # intercept
  
  for(i in 1:3){                                                  # prior for coefficients 
    betagamma.cl[i,k] ~ dnorm(0, 1)
  }
  
  # eps ~ season + popden + elevation + re.eps(area)
  
  alphaeps.cl[k] ~ dlogis(0, 1)                                         # intercept
  
  for(i in 1:3){                                                  # prior for coefficients 
    betaeps.cl[i,k] ~ dnorm(0, 1)
  }
  
  # p ~ season*habitat
  
  for (h in 1:4) {
    # alphap.cl[k,h] ~ dlogis(0, 1)                                      # intercept for each habitat type(4)
    
    alphap.cl[k,h] ~ dlogis(-2, 1)
    
    # betap values should be according to habitat types (AHM II Chp15 pp.104-105)
    # we have 4 habitat types --> 4 betap values for 1 betap category
    # we have 1 betap category: betap[p] season effect
    
    betap.cl[k,h] ~ dnorm(0, 1)                                        # betap season effect
  }
  
  # Random effect priors
  
  for (t in 1:(narea)){
    randompsi.cl[k,t] ~ dnorm(0, taupsi.cl[k])
    randomgamma.cl[k,t] ~ dnorm(0, taugamma.cl[k])
    randomeps.cl[k,t] ~ dnorm(0, taueps.cl[k])
    randomp.cl[k,t] ~ dnorm(0, taup.cl[k])
    
    
  }
  
  sigmapsi.cl[k] ~ dnorm(0, 1)T(0,)                      # Priors for standard deviations
  sigmagamma.cl[k] ~ dnorm(0, 1)T(0,)   
  sigmaeps.cl[k] ~ dnorm(0, 1)T(0,)   
  sigmap.cl[k] ~ dnorm(0, 1)T(0,)   
  
  taupsi.cl[k] <- pow(sigmapsi.cl[k], -2)
  taugamma.cl[k] <- pow(sigmagamma.cl[k], -2)
  taueps.cl[k] <- pow(sigmaeps.cl[k], -2)
  taup.cl[k] <- pow(sigmap.cl[k], -2)
  
  sigma2psi.cl[k] <- pow(sigmapsi.cl[k], 2)                  # Temporal variances
  sigma2gamma.cl[k] <- pow(sigmagamma.cl[k], 2)
  sigma2eps.cl[k] <- pow(sigmaeps.cl[k], 2)
  sigma2p.cl[k] <- pow(sigmap.cl[k], 2)
}

#### LIKELIHOOD ####

# Ecological submodel: intial occupancy as derived parameter

for (i in 1:nsite){
  z.cl[i,1] ~ dbern(psi1.cl[i])
  logit(psi1.cl[i]) <- alphapsi.cl[z.cc[site.cc[i],pocc.cc[i]]+1, habitat[i]] 
  + betapsi.cl[z.cc[site.cc[i],pocc.cc[i]]+1, habitat[i]]*popden[i]
  + randompsi.cl[z.cc[site.cc[i],pocc.cc[i]]+1, area[i]]
}

# State transitions: colonization and extinction
# for col and ext

for (i in 1:nsite){
  for (s in 2:nprimary){
    logit(gamma.cl[i,s-1]) <- alphagamma.cl[z.cc[site.cc[i],pocc.cc[i]]+1] 
    + betagamma.cl[1,z.cc[site.cc[i],pocc.cc[i]]+1]*season[i,s-1] + betagamma.cl[2,z.cc[site.cc[i],pocc.cc[i]]+1]*popden[i] 
    + betagamma.cl[3,z.cc[site.cc[i],pocc.cc[i]]+1]*season[i,s-1]*popden[i] + randomgamma.cl[z.cc[site.cc[i],pocc.cc[i]]+1,area[i]]
    
    logit(eps.cl[i,s-1]) <- alphaeps.cl[z.cc[site.cc[i],pocc.cc[i]]+1] 
    + betaeps.cl[1,z.cc[site.cc[i],pocc.cc[i]]+1]*season[i,s-1] + betaeps.cl[2,z.cc[site.cc[i],pocc.cc[i]]+1]*popden[i] 
    + betaeps.cl[3,z.cc[site.cc[i],pocc.cc[i]]+1]*season[i,s-1]*popden[i] + randomeps.cl[z.cc[site.cc[i],pocc.cc[i]]+1, area[i]]
  }
}

# for z: true occupancy

for(i in 1:nsite){
  for (t in 2:nprimary){
    
    z.cl[i,t] ~ dbern((z.cl[i,t-1]*(1-eps.cl[i,t-1])) + ((1-z.cl[i,t-1])*gamma.cl[i,t-1]))
  }
}

# Observation model 

for (i in 1:nobs.cl){
  logit(p.cl[i]) <- alphap.cl[z.cc[site.cc[i],pocc.cc[i]]+1,habitat.p.cl[i]]
  + betap.cl[z.cc[site.cc[i],pocc.cc[i]]+1,habitat.p.cl[i]]*season.p.cl[site.cl[i]]
  + randomp.cl[z.cc[site.cc[i],pocc.cc[i]]+1,area.cl[i]]
  
  # y.cl[i] ~ dbern(z.cl[site.cl[i],pocc.cl[i]]*p.cl[i])  # y: observed occupancy
  
  pstar.cl[i] <- 1-(1-p.cl[i])^ndays.cl[i] 
  y.cl[i] ~ dbern(z.cl[site.cl[i],pocc.cl[i]]*pstar.cl[i])  # y: observed occupancy
}


#### GOODNESS-OF-FIT GoF ####
# Based on posterior predictive distribution
# -------------------------------------------------------------------------

# derived occupancy state
for(i in 1:nsite){
 
 psi.cl[i, 1] <- psi1.cl[i] # Population occupancy as derived quantity
 for (t in 2:nprimary){
   
   psi.cl[i, t] <- (psi.cl[i,t-1]*(1-eps.cl[i,t-1])) + ((1-psi.cl[i,t-1])*gamma.cl[i,t-1])
 
 }
}

# Draw a replicate data set under the fitted model

for (i in 1:nobs.cl){
 
   yrep.cl[i] ~ dbern(z.cl[site.cl[i],pocc.cl[i]]*pstar.cl[i])
}

}